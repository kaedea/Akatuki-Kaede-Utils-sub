/*
 * Copyright (c) 2017. Kaede (kidhaibara@gmail.com) All Rights Reserved.
 */

def imports = { String path ->
    String name = path.substring(path.lastIndexOf('/') + 1)
    include(":$name")
    project(":$name").projectDir = file(path)
}

// ----------
// Application
// ----------

// Java
imports("applications/app.java_7")
imports("applications/app.java_8")
imports("applications/app.kotlin")

// Android
imports("applications/droid.feya")
imports("applications/droid.media")

// ----------
// Library
// ----------

// Archive Diff
imports("libraries/archive_diff/diff.shared")
imports("libraries/archive_diff/diff.sharedtest")
imports("libraries/archive_diff/diff.applier")
imports("libraries/archive_diff/diff.generator")

// Gradle Plugin
imports("plugins/plugin.greeting")
imports("plugins/plugin.requires")

// ----------
// Sub Module
// ----------

// "Nipaa" Repository
def nipaa = file('submodules/nipaa/settings.gradle')
if (nipaa.exists()) {
    apply from: nipaa
}