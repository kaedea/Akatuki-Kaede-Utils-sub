/*
 * Copyright (c) 2017. Kaede (kidhaibara@gmail.com) All Rights Reserved.
 */

ext.opt = { key ->
    // Find property in the following order:
    // 1. gradle.properties
    // 2. local.properties
    // 3. System environment
    (properties.get(key) ?: file('local.properties').with {
        if (it.exists()) {
            def pros = new Properties()
            pros.load(it.newDataInputStream())
            if (pros.getProperty(key)) return pros.getProperty(key)
        }
    } ?: System.getenv(key)).with {
        if (it == 'true' || it == 'false') return it.toBoolean()
    }
}

def imports = { String path ->
    String name = path.substring(path.lastIndexOf('/') + 1)
    include(":$name")
    project(":$name").projectDir = file(path)
}

// ----------
// Application
// ----------

// Java
imports("applications/app.java_7")
imports("applications/app.java_8")
imports("applications/app.kotlin")

// Android
if (opt('import.android')) {
    imports("applications/droid.feya")
    imports("applications/droid.media")
}

// ----------
// Library
// ----------

// Archive Diff
imports("libraries/archive_diff/diff.shared")
imports("libraries/archive_diff/diff.sharedtest")
imports("libraries/archive_diff/diff.applier")
imports("libraries/archive_diff/diff.generator")

// Gradle Plugin
if (opt('import.plugin')) {
    imports("plugins/plugin.greeting")
    imports("plugins/plugin.requires")
}

// ----------
// Sub Module
// ----------

// "Nipaa" Repository
def nipaa = file('submodules/nipaa/settings.gradle')
if (nipaa.exists()) {
    apply from: nipaa
}