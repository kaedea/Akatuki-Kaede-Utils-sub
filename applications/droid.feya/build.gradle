/*
 * Copyright (c) 2017. Kaede (kidhaibara@gmail.com) All Rights Reserved.
 */

apply plugin: 'com.android.application'
android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "me.kaede.feya"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        // multiDexEnabled true
        // testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        test {
            v2SigningEnabled false
            storeFile key_test['store.file']
            storePassword key_test['store.password']
            keyAlias key_test['key.alias']
            keyPassword key_test['key.password']
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.test
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.test
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    aaptOptions {
        noCompress "xml"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Test
    testCompile debugin['junit']
    androidTestCompile(debugin['espresso.core'], {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Android supports
    supports.each { k, v -> compile v }

    // Bolts
    compile frameworks['bolts_task']
    compile frameworks['bolts_link']

    // RX
    compile frameworks['rx_java']
    compile frameworks['rx_android']

    // BLog
    compile utils['b_log']

    // Retrofit
    compile frameworks['retrofit']
    compile frameworks['retrofit_fastjson']

    // Others
    compile utils['commons_lang']
    compile utils['commons_io']
    compile frameworks['wire_runtime']
    compile frameworks['butterknife']
    annotationProcessor frameworks['butterknife_apt']
}

//--------------------------------------------------------------------------------------------------
// Greeting
//--------------------------------------------------------------------------------------------------
buildscript {
    repositories {
        maven {
            url uri(rootProject.ext.localMaven)
        }
    }
    dependencies {
        if (rootProject.ext.greetPlugin) {
            classpath group: 'moe.studio.feya.greeting', name: 'greeting', version: '0.0.1'
        }
    }
}

if (rootProject.ext.greetPlugin) {
    apply plugin: 'moe.studio.feya.greeting'
    greeting {
        from = "David"
        message = "Hey, dude!"
        to {
            to = "Jager"
        }
    }
}
